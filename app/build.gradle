apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.0'
    dataBinding {
        enabled = true
    }
    defaultConfig {
        applicationId "se.oscarb.flowlist"
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 2
        versionName "0.2"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            applicationIdSuffix '.dev'
            versionNameSuffix '-DEV'
            getProperties('./src/debug/server.properties').each { p ->
                buildConfigField 'String', p.key, p.value
            }
            getProperties('./src/debug/facebook-app.properties').each { p ->
                resValue 'string', p.key, p.value
            }
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            getProperties('./src/main/server.properties').each { p ->
                buildConfigField 'String', p.key, p.value
            }

        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'com.facebook.android:account-kit-sdk:4.25.0'
    compile 'com.parse:parse-android:1.15.8'

    testCompile 'junit:junit:4.12'
}

def getProperties(path) {
    Properties props = new Properties()
    if (file(path).exists()) {
        props.load(new FileInputStream(file(path)))
        return props
    } else {
        throw new Exception("Could not find file: " + path)
    }
}
